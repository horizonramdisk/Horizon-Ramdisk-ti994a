********************************************************************************
* PRODIDS  - capture PFM product ID information   T.Tesch
*
* 9-9-2018  Program only displays info; not meant for anything else
*
* 3.26.2021 Separated from the display routines; common code for PFM Suite
*-------------------------------------------------------------------------------
*
****************
* Product ID   *
****************
*
* Return:   R0: 0000 = chip/ID not valid or regular EPROM
*               FFFF = valid
*
* Must be called from >0000->1FFF or >6000->FFFF
* as code pages in >2000->3FFF and >4000->5FFF for Identification Process
*
PRODR12     DATA 0       SAVE R12
PRODPAG     DATA 0       SAVE mapper F1/F2 here
PRODID#     DATA 0       Product ID Word  --> [Manufacturer ID | Device Code]
PFM_SECSIZE DATA 0
PFM_SECTORS DATA 0
PFM_TEXT    DATA 0       address of text
HAA         BYTE >AA
H55         BYTE >55
H90         BYTE >90
HF0         BYTE >F0

PFMDEV BYTE 13,10
       TEXT 'PFM Detected: '
       BYTE 0

PFMERR BYTE 13,10
       TEXT 'WARNING: No PFM Device Found'
       BYTE 13,10,0

*
* Chip Device Table
*           next,prodID,sectorsize,loops/8k, descriptor
PFMTBL DATA PFMT1,>1FD5,>0080,>1000
       TEXT 'Atmel 29c010 PFM+'
       BYTE 0
PFMT1  DATA PFMT2,>1FDA,>0100,>0800
       TEXT 'Atmel 29c020'
       BYTE 0
PFMT2  DATA PFMT3,>1F5B,>0200,>0400
       TEXT 'Atmel 29c040 PFM512'
       BYTE 0
PFMT3  DATA PFMT4,>1FA4,>0100,>0800
       TEXT 'Atmel 29c040a PFM512'
       BYTE 0
PFMT4  DATA PFMTND,>DA46,>0100,>0800         Winbond 512
       TEXT 'Winbond W29c040 PFM512'
       BYTE 0
PFMTND DATA 0      table end

PRODID MOV  R12,@PRODR12
       CLR  R12

       SBZ  >1D          ;4.1.2021, FORCE system chip active

       SBO  >15
       MOVB @>F111,@PRODPAG   save mapper
       MOVB @>F112,@PRODPAG+1 pages
       LI   R1,>F1F2
       MOVB R1,@>F111
       SWPB R1
       MOVB R1,@>F112
       MOVB @HAA,@>5555
       MOVB @H55,@>2AAA
       MOVB @H90,@>5555

;
;010 datasheet says 'pause 10ms' ; might need delay loop
;040 datasheet 10us
;
;       LI   R1,>FFFF
;DELP1  DEC  R1
;      JNE  DELP1

       SBZ >15

       MOV  @>4000,@PRODID#   Get the product ID

       SBO  >15
       SWPB R1
       SWPB R1
       MOVB @HAA,@>5555
       MOVB @H55,@>2AAA
       MOVB @HF0,@>5555
       SBZ  >15

       LI   R1,PFMTBL
NEXTID MOV  *R1+,R0      point to the product ID, R0 now holds next entry
       JEQ  IDEND        R0==0?  no more entries

       C    @PRODID#,*R1+  in table?
       JEQ  GOTID        this is a valid ID
       MOV  R0,R1
       JMP  NEXTID
GOTID  MOV  *R1+,@PFM_SECSIZE
       MOV  *R1+,@PFM_SECTORS
       MOV  R1,@PFM_TEXT      actual address, not indirect!

IDEND  MOVB @PRODPAG,@>F111
       MOVB @PRODPAG+1,@>F112
       MOV  @PRODR12,R12
       RT

;CRLF2  BYTE 13,10,0           *cr/llf combo for ttyout
;H32    BYTE >20
;HFF    BYTE >FF
;HC     BYTE >AA
;HD     BYTE >55
;HE     BYTE >A0
;      BYTE 0
;      EVEN
;eof
